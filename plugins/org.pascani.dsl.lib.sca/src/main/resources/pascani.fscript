function pascani-element-exists(selector) {
	return size($selector) > 0;
}

action pascani-add-intent(parent, target, intentName, routingKey) {
	-- 1. Create a new instance of the intent composite
	clone = sca-new($intentName);
	
	-- 2. Add the intent instance as a child of the target's parent
	add-scachild($parent, $clone);
	intent = $parent/scachild::$intentName;

	-- 3. Change the name of the intent component to be the routing key
	set-name($intent, $routingKey);
	intent = $parent/scachild::$routingKey;
	
	-- 4. Sets the routing key
	routingKeyProp = $intent/scachild::probe/scaproperty::routingKey;
	set-value($routingKeyProp, $routingKey);

	-- 5. Add the REST binding to the Resumable interface, and then add the SCA intent
	add-scaintent($target, $intent);
	
	-- 6. Wire the event handler service
	service = $intent/scachild::probe/scaservice::handler;
	reference = $intent/scachild::primitiveIntentHandler/scareference::handler;
	add-scawire($reference, $service);
	
	-- 7. Clean things up
	sca-remove($intentName); 
	set-state($intent, "STARTED");
	set-state($parent, "STARTED");
}

action pascani-remove-intent(parent, target, routingKey) {
	-- 1. Remove the SCA intent
	intent = $parent/scachild::$routingKey;
	remove-scaintent($target, $intent);
	
	set-state($intent, "STOPPED");
	set-state($parent, "STOPPED");
	-- TODO: remove all SCA services? (intent)
	
	-- 2. Remove the intent component from the target's parent
	remove-scachild($parent, $intent);
	set-state($parent, "STARTED");
}

action pascani-reset-probe(parent, routingKey) {
	intent = $parent/scachild::$routingKey;
	resetProbeProp = $intent/scachild::probe/scaproperty::resetProbe;
	set-value($resetProbeProp, true);
}

action pascani-reset-producer(parent, routingKey) {
	intent = $parent/scachild::$routingKey;
	resetProducerProp = $intent/scachild::probe/scaproperty::resetProducer;
	set-value($resetProducerProp, true);
}
