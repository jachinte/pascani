/*
 * Copyright © 2015 Universidad Icesi
 * 
 * This file is part of the Pascani project.
 * 
 * The Pascani project is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or (at your
 * option) any later version.
 * 
 * The Pascani project is distributed in the hope that it will be useful, but
 * WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
 * FITNESS FOR A PARTICULAR PURPOSE. See the GNU Lesser General Public License
 * for more details.
 * 
 * You should have received a copy of the GNU Lesser General Public License
 * along with The Pascani project. If not, see <http://www.gnu.org/licenses/>.
 */
grammar org.pascani.dsl.Pascani with org.eclipse.xtext.xbase.Xbase

import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types
import "http://www.eclipse.org/xtext/xbase/Xbase"

generate pascani "http://www.pascani.org/dsl/Pascani"

Model
    :	('package' name = QualifiedName ->';'?)? 
    	imports = XImportSection?
    	typeDeclaration = TypeDeclaration?
    ;

TypeDeclaration
	:	MonitorDeclaration 
    |	NamespaceDeclaration
    ;
    
MonitorDeclaration returns Monitor
	:	eventImports = ImportEventsSection?
		'monitor' name = ValidID 
		('using' usings += [Namespace | QualifiedName] (',' usings += [Namespace | QualifiedName])*)?  
		body = MonitorBlockExpression
	;

ImportEventsSection
	:	importDeclarations += ImportEventDeclaration+
	;
	
ImportEventDeclaration
	:	'from' monitor = [Monitor | QualifiedName] 'import' events += [Event | ID] (',' events += [Event | ID])*  ->';'?
	;
	
MonitorBlockExpression returns XBlockExpression
	:	{MonitorBlockExpression} '{' (expressions += InternalMonitorDeclaration)* '}'
	;

InternalMonitorDeclaration returns XExpression
	:	XVariableDeclaration ->';'?
	|	ConfigBlockExpression
	|	EventDeclaration
	|	HandlerDeclaration
	;
	
NamespaceDeclaration returns Namespace
	:	'namespace' name = ValidID body = NamespaceBlockExpression
	;

NamespaceBlockExpression returns XBlockExpression
	:	{NamespaceBlockExpression} '{' (expressions += InternalNamespaceDeclaration)* '}'
	;

InternalNamespaceDeclaration returns XExpression
	:	XVariableDeclaration ->';'?
	|	NamespaceDeclaration
	;

ConfigBlockExpression returns XBlockExpression
	:	{ConfigBlockExpression} 'config' '{' (expressions += XExpressionOrVarDeclaration ';'?)* '}'
	;

HandlerDeclaration returns Handler
	:	'handler' name = ValidID '(' param = FullJvmFormalParameter ')' body = XBlockExpression
	;

EventDeclaration returns Event
	:	'event' name = ValidID 'raised' (periodical ?= 'periodically')? 'on' emitter = EventEmitter ->';'?
	;

EventEmitter
	:	eventType = EventType 'of' emitter = XExpression (=> specifier = AndEventSpecifier)?
	|	cronExpression = XExpression
	;
	
enum EventType
	:	invoke
	|	return
	|	change
	|	exception
	;

AndEventSpecifier returns EventSpecifier
	:	OrEventSpecifier
		(
			{AndEventSpecifier.left = current} 
			operator='and' right = OrEventSpecifier
		)*
	;

OrEventSpecifier returns EventSpecifier
	:	SimpleEventSpecifier
		(
			{OrEventSpecifier.left = current} 
			operator='or' right = SimpleEventSpecifier
		)*
	;

SimpleEventSpecifier returns EventSpecifier
	:	(below ?= 'below' | above ?= 'above' | equal ?= 'equal' 'to') 
		value = XExpression (percentage ?= '%%')?
	|	'(' AndEventSpecifier ')'
	;

CronExpression
	:	lsymbol = '«'
			seconds 	= CronElement
			minutes 	= CronElement
			hours 		= CronElement
			dayOfMonth 	= CronElement
			month 		= CronElement
			dayOfWeek 	= CronElement
			(year 		= CronElement)?
		rsymbol = '»'
	;

CronElement
   :    CronElementList | IncrementCronElement | NthCronElement
   ;

/*
 * Options L and W of the Quartz scheduler are only supported
 * in cases were they are found alone (by means of rule ValidID).
 */ 
CronElementList
   :    elements += RangeCronElement (',' elements += RangeCronElement)*
   ;

IncrementCronElement
	:	start = TerminalCronElement ('-' end = TerminalCronElement)? '/' increment = TerminalCronElement
	;

RangeCronElement
   :    TerminalCronElement ({RangeCronElement.start = current} '-' end = TerminalCronElement)?
   ;

NthCronElement
	:	element = TerminalCronElement '#' nth = TerminalCronElement
	;

TerminalCronElement
   :    expression = (IntLiteral | ValidID | '*' | '?')
   ;

IntLiteral
	:	INT
	;

XLiteral returns XExpression:
	XCollectionLiteral |
	XClosure |
	XBooleanLiteral |
	XNumberLiteral |
	XNullLiteral |
	XStringLiteral |
	XTypeLiteral |
	CronExpression
;
